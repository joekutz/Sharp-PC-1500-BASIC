1 DATA 10,10,60,80,40,60,190,30,170,70
2 DATA 10,190,60,170,40,150,180,140,150,135
10 YS=0:SP=20:GRAPH:COLOR 2
20 FOR A = 1 TO 10 
21   LINE(0,YS)-(210,YS)
22   YS = YS + SP
23   SP = INT(SP / 1.25)
30 NEXT A
40 YS=0:SP=20:COLOR 3
50 FOR A = 1 TO 10 
51   LINE(0,200-YS)-(210,200-YS)
52   YS = YS + SP
53   SP = INT(SP / 1.25)
60 NEXT A
65 COLOR 0
70 FOR A = 1 TO 5 
71   READ HX, HY
72   SC = (2 / (HY / 10) ) + 0.5
73   GOSUB"HOUSE"
75 NEXT A
76 COLOR 1
80 FOR A = 1 TO 5 
81   READ HX, HY
82   SC = INT(HY/30)-3
83   GOSUB"CLOUD"
85 NEXT A
86 GLCURSOR(0,-10)
90 LPRINT("(c)2025 JoE")
100 END
300"RECT"
310 LINE (RX,RY)-(RX+RW,RY)
320 LINE (RX+RW,RY)-(RX+RW,RY+RH)
330 LINE (RX+RW,RY+RH)-(RX,RY+RH)
340 LINE (RX,RY+RH)-(RX,RY)
350 RETURN
400"HOUSE"
410 RX=HX:RY=HY:RW=16*SC:RH=16*SC:GOSUB"RECT"
420 RX=HX+2*SC:RY=HY+5*SC:RW=4*SC:RH=6*SC:GOSUB"RECT"
430 RX=HX+10*SC:RY=HY:RW=4*SC:RH=11*SC:GOSUB"RECT"
440 LINE (HX-3*SC, HY+13*SC)-(HX+8*SC,HY+23*SC)
450 LINE (HX+8*SC, HY+23*SC)-(HX+20*SC,HY+13*SC)
460 RX=HX+3*SC:RY=HY+18*SC:RW=2*SC:RH=6*SC:GOSUB"RECT"
470 RETURN
500"CLOUD"
505 CSIZE SC
510 GLCURSOR(HX,HY):LPRINT"O"
520 GLCURSOR(HX+4*SC,HY+6*SC):LPRINT"O"
530 GLCURSOR(HX+8*SC,HY):LPRINT"O"
540 GLCURSOR(HX+16*SC,HY):LPRINT"O"
550 GLCURSOR(HX+12*SC,HY+6*SC):LPRINT"O"
560 RETURN
