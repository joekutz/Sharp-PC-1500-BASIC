1 REM Coordinate Data (x1,y1,x2.y2..)
2 DATA 11,22,5,21,12,20,14,23,10,16,13,19,16,18,9,26,8,15
3 DATA 7,17,6,25,15,13,17,27,19,24,4,11,22,37,26,29,27,36
4 DATA 28,31,29,39,20,32,24,34,25,30,30,35,23,33,21,40,19,42
5 DATA 31,38,32,28,18,41,32,12,29,11,28,22,35,13,36,18,26,19
6 DATA 34,17,30,14,33,16,27,15,31,7,25,20,23,9,37,8,38,10
9 LET U = 0:REM 1st plot only black
10 LET NP = 45:LET NC = 3
12 DIM CT(NP):DIM XC(NC), YC(NC)
14 LET D1 = 0:D2 = 0
15 LET OD = 0:ND = 0
20 XCE(1)=RND50:XCE(2)=RND50:XCE(3)=RND50
30 YCE(1)=RND50:YCE(2)=RND50:YCE(3)=RND50
50 TEXT:LPRINT"K-Means (c)2025"
51 LPRINT"------JOE------"
52 LPRINT"Initial clusters"
53 LPRINT"and random centers"
54 FOR A = 1 TO NP
55   CT(A) = 0
56 NEXT A
60 GOSUB"PRINTGR"
61 U=NC:REM now print all cl
65 IT = 1
70 GOSUB"ESTEP"
71 "ITERLOOP":OD=ND:IT = IT + 1
72 GOSUB"MSTEP"
73 GOSUB"ESTEP"
74 IF ND < OD THEN GOTO "ITERLOOP"
75 GOSUB"PRINTGR"
100END

300 "ESTEP"
301 LPRINT "Iteration " + STR$(IT)
305 RESTORE:ND = 0
310 FOR A = 1 TO NP
311   READ X,Y
312   D1 = SQR(ABS(X-XC(1))^2 + ABS(Y-YC(1))^2)
313   WD = 1 
314   FOR B = 2 TO NC
315     D2 = SQR(ABS(X-XC(B))^2 + ABS(Y-YC(B))^2)
316     IF D2 > D1 THEN "NOTSMALLER"
317        D1 = D2
318        WD = B
319     "NOTSMALLER"REM not smaller
320   NEXT B
321   ND = ND + D1
322   CT(A) = WD
325 NEXT A           
326 LPRINT"Avg. Error:" + STR$(INT(10 * ND / NP)/10)
330 RETURN

400 "MSTEP":REM mstep
410 FOR B = 1 TO NC
415   MX=0:MY=0:CC=0
420   RESTORE
425   FOR A = 1 TO NP
426     READ X,Y
427     IF CT(A) <> B THEN GOTO "WRONGCL"
428       MX = MX + X
429       MY = MY + Y
430       CC = CC + 1
431     "WRONGCL":REM wrong cluster
435   NEXT A
436   IF NCL = 0 THEN GOTO "EMPTYCLUSTER"
437   XC(B) = INT(MX / CC)
438   YC(B) = INT(MY / CC)
439 "EMPTYCLUSTER"REM cl empty
450 NEXT B
460 RETURN 

500 "PRINTGR":GRAPH:GLCURSOR(10,-220):GLCURSOR(10,0):SORGN
505 GLCURSOR(-10,0):LPRINT 50
510 LINE(0,0)-(0,-200),0,0
515 GLCURSOR(-10,-220):LPRINT 0
520 LINE(0,-200)-(200,-200),0,0
525 GLCURSOR(160,-220):LPRINT 50
530 FOR A = 200 TO 0 STEP -40
531   LINE(A,-198)-(A,-210)
532 NEXTA
540 FOR A = -200 TO 0 STEP 40
541   LINE(-10,A)-(2,A)
542 NEXTA
545 GLCURSOR(-10,-220):SORGN
550 FOR M = U TO 0 STEP -1
551   RESTORE
552   COLOR M
553   FOR A = 1 TO NP
554     READ X,Y
555     IF CT(A) <> M THEN GOTO "NOPRINT"
558       GLCURSOR(4*X-5,4*Y+15)
559       LPRINT"x"
560     "NOPRINT"REM "no print"
565   NEXT A
570 NEXT M
572 FOR A = 1 TO NC
573   GLCURSOR(4*XC(A)-5,4*YC(A)+15)
574   LPRINT"o"
575 NEXT A
576 GLCURSOR(0,0):TEXT:LF1
580 RETURN
